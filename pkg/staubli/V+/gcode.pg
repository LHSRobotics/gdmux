AUTO slun
AUTO REAL x, y, z, a, b, c, op

auto starta, enda
auto angelstep
auto angle, radius, zstep
		
ATTACH (slun, 4) "SERIAL:1"

SELECT ROBOT = 1
ATTACH ()
SPEED 40 ALWAYS
ACCEL (1) 100, 100

SET zeroloc = TRANS(500,0,0,0,90,0)
ABOVE
IF INRANGE(zeroloc) == 0 THEN
	MOVE zeroloc
	WRITE (slun) "Ready"
ELSE
	READY
	DRIVE 2, 45, 100
	WRITE (slun) "Ready"
END
ABOVE

WHILE TRUE DO
	READ (slun) op, a, b, c, d, e, f, g
	IF IOSTAT(slun) < 0 THEN
		TYPE IOSTAT(slun), " ", $ERROR(IOSTAT(slun))
		TIMER 1 = 0
		WAIT TIMER(1) > 1
	END

	CASE op OF
	VALUE 0:
		; quick move
		x = a
		y = b
		z = c
		SET loc = TRANS(x,y,z,0,90,0)

		TYPE "move ", x, ",", y, ",", z
		IF INRANGE(loc) == 0 THEN
			MOVE loc
			BREAK
			WRITE (slun) "OK"
		ELSE
			TYPE "out of range"
			WRITE (slun) "out of range"
		END
	VALUE 1:
		; straight line
		x = a
		y = b
		z = c
		SET loc = TRANS(x,y,z,0,90,0)

		TYPE "line ", x, ",", y, ",", z
		IF INRANGE(loc) == 0 THEN
			MOVES loc
			BREAK
			WRITE (slun) "OK"
		ELSE
			TYPE "out of range"
			WRITE (slun) "out of range"
		END
	VALUE 2:
		; arc
		target.x = a
		target.y = b
		target.z = c
		
		center.x = d
		center.y = e
		center.z = f
		
		starta = atan2(y - center.y,x - center.x)
		
		TYPE "start: ", y - center.y, ", ", x - center.x
		
		enda = atan2(target.y - center.y, target.x - center.x)
		
		TYPE "end: ", target.y - center.y, ", ", target.x - center.x
		
		
		; TODO make sure the radius is the same for both
		radius = sqrt(sqr(x - center.x) + sqr(y - center.y))
		
		TYPE "arc ", target.x, ",", target.y, ",", target.z
		TYPE "around ", center.x, ",", center.y, ",", center.z
		DURATION 0.5 ALWAYS
		
		angelstep = g
		
		travel = (target.z-z)
		rate = abs(angelstep/(enda-starta))
		
		zstep = travel * rate
		
		TYPE "start angle: ", starta, " end angle: ", enda, " radius: ", radius, " zstep: ", zstep
		
		arclength = abs((enda - starta)/angelstep)
		
		FOR i = 0 TO arclength
			angle = angelstep * i + starta
			
			x = radius * COS(angle) + center.x
			y = radius * SIN(angle) + center.y
			
			z = z + zstep
		
			SET loc = TRANS(x,y,z,0,90,0)
			
			IF INRANGE(loc) == 0 THEN
				MOVEs loc
			ELSE
				TYPE "arc part out of range"
			END
		END
		;BREAK
		WRITE (slun) "OK"
	ANY
		TYPE "unknown opcode"
		WRITE (slun) "unknown opcode"
	END

END
